buildscript { // dependencies for the gradle build file (not for the Java project itself!)
    dependencies {
        classpath files('my-plugin/build/libs/my-plugin.jar')
    }
}

plugins {
    id 'java'
}

group 'top.kerstholt.gradle'
version '1.0.0'

sourceCompatibility = 1.11

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile project(':json-display')
}

apply plugin: 'project-report'

// Below follow 3 examples of hand-writing a custom task.
// Example 1: Task provided by our custom plugin (myPluginTask)
apply plugin: 'my-custom-plugin-id' // Refers to my-plugin.jar/META-INF/<id>.properties

// Example 2: Custom task, written in DSL
task showDate {
    // This triggers the 'build' task before 'showDate' is executed.
    dependsOn build

    // Grouping (visible in 'gradle tasks' or in gradle view in IntelliJ)
    group = 'My custom tasks'
    description = 'Show current date'

    doLast {
        println 'Current date: ' + new Date()
    }
}

// Example 3: Custom task, written in Groovy
class HelloWorld extends DefaultTask {

    @Override
    String getGroup() {
        return "My custom tasks"
    }

    @Override
    String getDescription() {
        return "Hello World"
    }

    @TaskAction
    void hello() {
        println "Hello World!"
    }
}

task hello(type: HelloWorld)
